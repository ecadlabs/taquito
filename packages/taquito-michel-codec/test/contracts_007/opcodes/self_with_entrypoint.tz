parameter (or (or (nat %A) (bool %B)) (or %maybe_C (unit %Z) (string %C)));
storage unit;
code {
       DROP;
       # Refers to entrypoint A of the current contract.
       SELF %A; PACK @Apacked;
       # Refers to the default entry of the current contract
       SELF %default; PACK @defpacked; DUP; DIP { SWAP }; ASSERT_CMPNEQ;
       # "SELF" w/o annotation also refers to the default
       # entry of the current contract
       SELF; PACK @selfpacked; ASSERT_CMPEQ;

       # Verify the types of the different entrypoints.  CAST is noop
       # if its argument is convertible with the type of the top of
       # the stack.
       SELF %A; CAST (contract nat); DROP;
       SELF %B; CAST (contract bool); DROP;
       SELF %maybe_C; CAST (contract (or (unit) (string))); DROP;
       SELF %Z; CAST (contract unit); DROP;
       SELF; CAST (contract (or (or (nat %A) (bool %B)) (or %maybe_C (unit %Z) (string %C)))); DROP;
       SELF %default; CAST (contract (or (or (nat %A) (bool %B)) (or %maybe_C (unit %Z) (string %C)))); DROP;

       UNIT;
       NIL operation;
       PAIR;
     }
