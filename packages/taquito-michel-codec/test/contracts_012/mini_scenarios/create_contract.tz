/*
- param: None:

  Create a contract then perform a recursive call on Some [addr] where
  [addr] is the address of the newly created contract.

  The created contract simply stores its parameter (a string).  It is
  initialized with the storage "dummy" and has an initial balance of
  100tz. It has no delegate so these 100tz are totally frozen.

- param: Some [addr]:

  Check that the sender is self, call the contract at address [addr]
  with param "abcdefg" transferring 0tz.

*/
parameter (option address) ;
storage unit ;
code { CAR ;
       IF_NONE
         { PUSH string "dummy" ;
           PUSH mutez 100000000 ; NONE key_hash ;
           CREATE_CONTRACT
             { parameter string ;
               storage string ;
               code { CAR ; NIL operation ; PAIR } } ;
           DIP { SOME ; DIP { SELF ; PUSH mutez 0 } ; TRANSFER_TOKENS ;
                 NIL operation ; SWAP ; CONS } ;
           CONS ; UNIT ; SWAP ; PAIR }
         { SELF ; ADDRESS ; SENDER ; IFCMPNEQ { FAIL } {} ;
           CONTRACT string ; IF_SOME {} { FAIL } ;
           PUSH mutez 0 ; PUSH string "abcdefg" ; TRANSFER_TOKENS ;
           NIL operation; SWAP; CONS ; UNIT ; SWAP ; PAIR } } ;